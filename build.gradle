plugins {
    id 'org.springframework.boot' version '2.1.5.RELEASE'
    id 'java'
    id 'io.franzbecker.gradle-lombok' version '1.14'
    id 'io.spring.dependency-management' version '1.0.7.RELEASE'
}

lombok {
    version = '1.18.4'
}

repositories {
    mavenCentral()
}

dependencies {
    compile('org.springframework.boot:spring-boot-starter-actuator')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile group: 'io.springfox', name: 'springfox-swagger2', version: '2.6.1'
    compile group: 'io.springfox', name: 'springfox-swagger-ui', version: '2.6.1'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.9'
    testCompile 'org.springframework.boot:spring-boot-starter-test'
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

sourceSets {
    integrationTest {
        java {
            srcDir file('src/integrationtest/java')
            compileClasspath += sourceSets.main.output + configurations.testRuntime
            runtimeClasspath += output + compileClasspath
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

task integrationTest(type: Test) {
    group = LifecycleBasePlugin.VERIFICATION_GROUP
    description = 'Runs the integration tests.'
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }

    dependsOn tasks.compileJava
    dependsOn tasks.compileTestJava
    dependsOn tasks.compileIntegrationTestJava
}

test {
    testLogging {
        events 'PASSED', 'STARTED', 'FAILED', 'SKIPPED'
        exceptionFormat 'full'
        showStandardStreams = true
    }
}

integrationTest {
    testLogging {
        events 'PASSED', 'STARTED', 'FAILED', 'SKIPPED'
        exceptionFormat 'full'
        showStandardStreams = true
    }
}
